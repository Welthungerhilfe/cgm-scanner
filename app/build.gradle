apply plugin: 'com.android.application'

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "de.welthungerhilfe.cgm.scanner"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 716
        versionName "7.4.0-dev"
        manifestPlaceholders = [arCameraRequired:"false", depthCameraRequired:"false", arcoreValue:"optional", arengineValue:"optional"]
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":"$projectDir/schemas".toString()]
            }
        }
    }

    allprojects {
        repositories {
            jcenter()
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    dataBinding {
        enabled = true
    }
    flavorDimensions "platform"
    productFlavors {
        arcore {
            dimension "platform"
            minSdkVersion 28
            manifestPlaceholders = [arCameraRequired:"true", depthCameraRequired:"true", arcoreValue:"required", arengineValue:"optional"]
        }
        arengine {
            dimension "platform"
            minSdkVersion 28
            manifestPlaceholders = [arCameraRequired:"true", depthCameraRequired:"false", arcoreValue:"optional", arengineValue:"required"]
        }
        nonar {
            dimension "platform"
            minSdkVersion 23
            manifestPlaceholders = [arCameraRequired:"false", depthCameraRequired:"false", arcoreValue:"optional", arengineValue:"optional"]
        }
    }

    testOptions.unitTests.includeAndroidResources = true
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    lintOptions {
        abortOnError true
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {

    // Android core libs
    //noinspection LifecycleAnnotationProcessorWithJava8
    annotationProcessor 'androidx.lifecycle:lifecycle-compiler:2.2.0' //android lifecycle compiler
    implementation 'androidx.lifecycle:lifecycle-runtime:2.2.0' //android lifecycle base
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0' //android lifecycle extensions
    implementation 'com.google.android.material:material:1.1.0' //app layout theme
    implementation 'androidx.multidex:multidex:2.0.1' //support more dex files in one APK
    implementation 'io.reactivex.rxjava3:rxjava:3.0.0' //RxJava core
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0' //RxJava for Android
    implementation 'androidx.work:work-runtime:2.3.3' //workmanager for background task

    // Camera
    implementation 'com.google.ar:core:1.30.0' //Google ARCore
    implementation 'com.huawei.hms:arenginesdk:2.15.0.4' //Huawei AREngine
    implementation 'me.dm7.barcodescanner:zxing:1.9.8' //QR Code scanner

    // Recording audio
    implementation 'org.jcodec:jcodec:0.2.3'
    implementation 'org.jcodec:jcodec-android:0.2.3'

    // Network
    implementation "com.microsoft.appcenter:appcenter-analytics:2.5.0" //AppCenter analytics
    implementation "com.microsoft.appcenter:appcenter-crashes:2.5.0" //AppCenter crash reports
    implementation 'androidx.room:room-runtime:2.2.5' //database syntax helper
    implementation 'com.android.volley:volley:1.1.1' //Authentication answer parser
    implementation 'com.microsoft.identity.client:msal:2.0.5' //Microsoft authentication
    annotationProcessor 'androidx.room:room-compiler:2.2.5' //database syntax compiler
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.4' //database debugging
    implementation 'com.squareup.retrofit2:retrofit:2.9.0' //RestAPI core package
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0' //RestAPI gson converter
    implementation 'com.squareup.retrofit2:adapter-rxjava3:2.9.0' //RestAPI RxJava integration
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.0' //RestAPI interceptor

    // Tests
    testImplementation 'junit:junit:4.12' //java unit test
    testImplementation 'org.mockito:mockito-core:2.19.0' //app mocking
    androidTestImplementation 'androidx.room:room-testing:2.2.5' //database testing
    testImplementation 'org.hamcrest:hamcrest-library:1.3'//framework for software tests
    testImplementation "org.robolectric:robolectric:4.2.1"//write unit tests and run them on a desktop JVM
    testImplementation 'androidx.test:core:1.3.0'
    testImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation 'androidx.test.espresso:espresso-contrib:3.4.0'
    testImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    testImplementation 'androidx.test.espresso:espresso-intents:3.4.0'

    // UI items
    implementation 'com.balysv:material-ripple:1.0.2' //custom tab (e.g.in scan mode)
    implementation 'com.appeaser.sublimepickerlibrary:sublimepickerlibrary:2.1.1' //date selector
    implementation 'com.orhanobut:dialogplus:1.11@aar' //filter popup (scans tab)
    implementation 'com.google.android.gms:play-services-location:17.0.0' //google location
    implementation 'com.google.android.gms:play-services-maps:17.0.0' //google maps
    implementation 'com.jaredrummler:material-spinner:1.2.4' //growth chart type selector
    implementation 'com.google.guava:guava:28.1-android' //growth chart data iterator
    implementation 'com.github.bumptech.glide:glide:4.11.0' //image preview
    implementation 'com.romandanylyk:pageindicatorview:1.0.2' //indicator of current page (in tutorial)
    implementation 'com.shuhart.stepview:stepview:1.5.1' //for showing steps(in tutorial)
    implementation 'id.zelory:compressor:2.1.0' //ImageCompression library


    //Firebase
    implementation platform('com.google.firebase:firebase-bom:28.4.2')// When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-analytics' // Declare the dependency for the Firebase SDK for Google Analytics

}
apply plugin: 'com.google.gms.google-services'
