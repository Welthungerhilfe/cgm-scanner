apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "de.welthungerhilfe.cgm.scanner"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 301
        versionName "3.0.2-rc2"
        manifestPlaceholders = [arCameraRequired:"false", depthCameraRequired:"false", arcoreValue:"optional", arengineValue:"optional", tangoRequired:"false"]
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":"$projectDir/schemas".toString()]
            }
        }
    }
    allprojects {
        repositories {
            jcenter()
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    dataBinding {
        enabled = true
    }
    flavorDimensions "platform"
    productFlavors {
        arcore {
            dimension "platform"
            minSdkVersion 28
            manifestPlaceholders = [arCameraRequired:"true", depthCameraRequired:"false", arcoreValue:"required", arengineValue:"optional", tangoRequired:"false"]
        }
        arengine {
            dimension "platform"
            minSdkVersion 28
            manifestPlaceholders = [arCameraRequired:"true", depthCameraRequired:"false", arcoreValue:"optional", arengineValue:"required", tangoRequired:"false"]
        }
        tango {
            dimension "platform"
            minSdkVersion 23
            manifestPlaceholders = [arCameraRequired:"false", depthCameraRequired:"false", arcoreValue:"optional", arengineValue:"optional", tangoRequired:"true"]
        }
        nonar {
            dimension "platform"
            minSdkVersion 28
            manifestPlaceholders = [arCameraRequired:"false", depthCameraRequired:"false", arcoreValue:"optional", arengineValue:"optional", tangoRequired:"false"]
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    lintOptions {
        abortOnError true
    }
}

dependencies {

    // Android core libs
    //noinspection LifecycleAnnotationProcessorWithJava8
    annotationProcessor 'androidx.lifecycle:lifecycle-compiler:2.2.0' //android lifecycle compiler
    implementation 'androidx.lifecycle:lifecycle-runtime:2.2.0' //android lifecycle base
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0' //android lifecycle extensions
    implementation 'com.google.android.material:material:1.1.0' //app layout theme
    implementation 'androidx.multidex:multidex:2.0.1' //support more dex files in one APK
    implementation 'com.jakewharton:butterknife:10.2.1' //helper to link UI item easily from code
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.1' //compiler to link UI item easily from code
    implementation 'com.google.dagger:dagger:2.13' //dependecy injection helper
    annotationProcessor 'com.google.dagger:dagger-compiler:2.13' //dependency injection compiler

    // Camera
    implementation 'com.google.ar:core:1.18.0' //Google ARCore
    implementation 'com.google.tango:sdk-base:1.55' //Google Tango main
    implementation 'com.google.tango:support-base:1.54' //Google Tango helper
    implementation 'com.huawei.hms:arenginesdk:2.13.0.4' //Huawei AREngine
    implementation 'me.dm7.barcodescanner:zxing:1.9.8' //QR Code scanner

    // Network
    implementation "com.microsoft.appcenter:appcenter-auth:2.5.0" //AppCenter authorisation
    implementation "com.microsoft.appcenter:appcenter-analytics:2.5.0" //AppCenter analytics
    implementation "com.microsoft.appcenter:appcenter-crashes:2.5.0" //AppCenter crash reports
    implementation 'com.microsoft.azure.android:azure-storage-android:2.0.0@aar' //Azure storage
    implementation 'com.novoda:merlin:1.1.7' //connection timestamping
    implementation 'androidx.room:room-runtime:2.2.5' //database syntax helper
    annotationProcessor 'androidx.room:room-compiler:2.2.5' //database syntax compiler
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.4' //database debugging

    // Tests
    testImplementation 'junit:junit:4.12' //java unit test
    testImplementation 'org.mockito:mockito-core:2.19.0' //app mocking
    androidTestImplementation 'androidx.room:room-testing:2.2.5' //database testing
    androidTestImplementation 'androidx.test.ext:junit:1.1.1' //extended java unit test functionality

    // UI items
    implementation 'com.balysv:material-ripple:1.0.2' //custom tab (e.g.in scan mode)
    implementation 'com.appeaser.sublimepickerlibrary:sublimepickerlibrary:2.1.1' //date selector
    implementation 'com.orhanobut:dialogplus:1.11@aar' //filter popup (scans tab)
    implementation 'com.google.android.gms:play-services-location:17.0.0' //google location
    implementation 'com.google.android.gms:play-services-maps:17.0.0' //google maps
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3' //growth chart graph
    implementation 'com.jaredrummler:material-spinner:1.2.4' //growth chart type selector
    implementation 'com.google.guava:guava:28.1-android' //growth chart data iterator
    implementation 'com.github.bumptech.glide:glide:4.11.0' //image preview
    implementation 'com.romandanylyk:pageindicatorview:1.0.2' //indicator of current page (in tutorial)
}
apply plugin: 'com.google.gms.google-services'
